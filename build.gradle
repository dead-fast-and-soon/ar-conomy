import org.ajoberstar.grgit.*
import org.apache.tools.ant.filters.ReplaceTokens

ext.proj_name = 'ARconomy'

getGitVersion()

/**
 * generates project version using the format
 * <git_tag>-<git_hash>
 */
def getGitVersion() {
	Grgit grgit = Grgit.open(project.file("."))

	if(grgit == null) {
		project.version = "unknown"
		return
	}

	//=============================
	// retrieve tag head

	List<Tag> tags = grgit.tag.list()
	Tag lastTag = null

	try {
		// find the tag that matches git repo head
		lastTag = tags.find{it.commit == grgit.head()}
	} catch (Exception ignored) {
		// get the last tag instead
		lastTag = tags.getAt(tags.size() - 1)
	}

	def tagName = null
	if(lastTag != null) {
		tagName = lastTag.getName()
		println "using tag: " + tagName
	}

	//=============================
	// retrieve hash for commit head

	def commitHash = grgit.head().abbreviatedId

	println "using commit: " + commitHash

	if(lastTag != null) {
		project.ext.proj_version = tagName + "-" + commitHash
	} else {
		project.ext.proj_version = commitHash
	}
}

buildscript {
	repositories {
		mavenCentral()
		jcenter()
	}

	dependencies {
		classpath "com.github.jengelman.gradle.plugins:shadow:1.2.3"
		classpath "org.ajoberstar:gradle-git:1.3.2"
	}

}

subprojects {
	apply plugin: "java"
	apply plugin: "idea"
	apply plugin: "com.github.johnrengelman.shadow"

	group = 'org.hyperfresh.arconomy'
	version = proj_version

	sourceCompatibility = 1.7
	targetCompatibility = 1.7

	jar {
		manifest {
			attributes "Main-Class": "org.hyperfresh.arconomy.Main"
		}
	}

	task copyDependencies(type: Copy) {
		from configurations.runtime
		into "$buildDir/libs"
	}

	compileJava.dependsOn copyDependencies

	repositories {
		mavenCentral()

		// Repository for Bukkit
		maven { url "https://hub.spigotmc.org/nexus/content/groups/public/" }

		// Repository for Vault
		maven { url "http://nexus.theyeticave.net/content/repositories/pub_releases/" }
	}

	dependencies {
		testCompile "junit:junit:4.12"
	}

	shadowJar {
		classifier ''
	}

	processResources {
		filter ReplaceTokens, tokens:[NAME: project.proj_name, VERSION: project.proj_version]
	}


}
